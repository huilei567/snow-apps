# The latest ref in branch stable
set(ref 31e19f92f00c7003fa115047ce50978bc98c3a0d)

# Note on x264 versioning:
# The pc file exports "0.164.<N>" where is the number of commits.
# The binary releases on https://artifacts.videolan.org/x264/ are named x264-r<N>-<COMMIT>.
# With a git clone, this can be determined by running `versions.sh`.
# For --editable mode, use configured patch instead of vcpkg_replace_string.
string(REGEX MATCH "^......." short_ref "${ref}")
string(REGEX MATCH "[0-9]+\$" revision "${VERSION}")
configure_file("${CURRENT_PORT_DIR}/version.diff.in" "${CURRENT_BUILDTREES_DIR}/src/version-${VERSION}.diff" @ONLY)

# Snow Apps fork: clone over git instead of downloading the GitLab archive.
# code.videolan.org now answers archive requests with an anti-bot challenge page,
# so the tarball hash can never match; the git transport is unaffected and the
# commit SHA below already pins the exact revision.
vcpkg_from_git(
    URL https://code.videolan.org/videolan/x264.git
    OUT_SOURCE_PATH SOURCE_PATH
    REF "${ref}"
    HEAD_REF master
    PATCHES
        "${CURRENT_BUILDTREES_DIR}/src/version-${VERSION}.diff"
        uwp-cflags.patch
        parallel-install.patch
        allow-clang-cl.patch
        configure.patch
)

function(add_cross_prefix)
  if(configure_env MATCHES "CC=([^\\/]*-)gcc$")
      vcpkg_list(APPEND arg_OPTIONS "--cross-prefix=${CMAKE_MATCH_1}")
  endif()
  set(arg_OPTIONS "${arg_OPTIONS}" PARENT_SCOPE)
endfunction()

function(snow_x264_short_path out_var path)
    file(TO_NATIVE_PATH "${path}" _snow_native_path)
    execute_process(
        COMMAND "$ENV{COMSPEC}" /d /c for %I in ("${_snow_native_path}") do @echo %~sI
        RESULT_VARIABLE _snow_short_path_result
        OUTPUT_VARIABLE _snow_short_path
        ERROR_VARIABLE _snow_short_path_error
        OUTPUT_STRIP_TRAILING_WHITESPACE
    )
    if(NOT _snow_short_path_result EQUAL 0 OR _snow_short_path STREQUAL "")
        message(FATAL_ERROR
            "Unable to resolve an x264 shell-safe path for '${path}': ${_snow_short_path_error}")
    endif()
    file(TO_CMAKE_PATH "${_snow_short_path}" _snow_short_path)
    set("${out_var}" "${_snow_short_path}" PARENT_SCOPE)
endfunction()

function(snow_x264_short_path_list out_var paths)
    set(_snow_short_paths)
    foreach(_snow_path IN LISTS paths)
        if(NOT _snow_path STREQUAL "")
            snow_x264_short_path(_snow_short_path "${_snow_path}")
            list(APPEND _snow_short_paths "${_snow_short_path}")
        endif()
    endforeach()
    list(JOIN _snow_short_paths ";" _snow_short_paths)
    set("${out_var}" "${_snow_short_paths}" PARENT_SCOPE)
endfunction()

set(nasm_archs x86 x64)
set(gaspp_archs arm arm64)
if(NOT "asm" IN_LIST FEATURES)
    vcpkg_list(APPEND OPTIONS --disable-asm)
elseif(NOT "$ENV{AS}" STREQUAL "")
    # Accept setting from triplet
elseif(VCPKG_TARGET_ARCHITECTURE IN_LIST nasm_archs)
    vcpkg_find_acquire_program(NASM)
    vcpkg_insert_program_into_path("${NASM}")
    set(ENV{AS} "${NASM}")
elseif(VCPKG_TARGET_ARCHITECTURE IN_LIST gaspp_archs AND VCPKG_TARGET_IS_WINDOWS AND VCPKG_HOST_IS_WINDOWS)
    vcpkg_find_acquire_program(GASPREPROCESSOR)
    list(FILTER GASPREPROCESSOR INCLUDE REGEX gas-preprocessor)
    file(INSTALL "${GASPREPROCESSOR}" DESTINATION "${SOURCE_PATH}/tools" RENAME "gas-preprocessor.pl")
endif()

vcpkg_list(SET OPTIONS_RELEASE)
if("tool" IN_LIST FEATURES)
    vcpkg_list(APPEND OPTIONS_RELEASE --enable-cli)
else()
    vcpkg_list(APPEND OPTIONS_RELEASE --disable-cli)
endif()

if("chroma-format-all" IN_LIST FEATURES)
    vcpkg_list(APPEND OPTIONS --chroma-format=all)
endif()

if(NOT "gpl" IN_LIST FEATURES)
    vcpkg_list(APPEND OPTIONS --disable-gpl)
endif()

if(VCPKG_TARGET_IS_UWP)
    list(APPEND OPTIONS --extra-cflags=-D_WIN32_WINNT=0x0A00)
endif()

if(VCPKG_TARGET_IS_OSX AND VCPKG_TARGET_ARCHITECTURE STREQUAL "arm64")
    list(APPEND OPTIONS
        "--extra-asflags=-mmacosx-version-min=${VCPKG_OSX_DEPLOYMENT_TARGET}")
endif()

if(VCPKG_TARGET_IS_WINDOWS AND NOT VCPKG_TARGET_IS_MINGW)
    foreach(_snow_env_var IN ITEMS INCLUDE LIB LIBPATH)
        if(DEFINED ENV{${_snow_env_var}})
            set("_snow_x264_${_snow_env_var}_was_defined" TRUE)
            set("_snow_x264_${_snow_env_var}" "$ENV{${_snow_env_var}}")
        else()
            set("_snow_x264_${_snow_env_var}_was_defined" FALSE)
        endif()
        snow_x264_short_path_list(_snow_short_paths "$ENV{${_snow_env_var}}")
        if(_snow_short_paths STREQUAL "")
            unset(ENV{${_snow_env_var}})
        else()
            set(ENV{${_snow_env_var}} "${_snow_short_paths}")
        endif()
    endforeach()

    if(DEFINED ENV{MSYS2_ENV_CONV_EXCL})
        set(_snow_x264_msys2_env_conv_excl_was_defined TRUE)
        set(_snow_x264_msys2_env_conv_excl "$ENV{MSYS2_ENV_CONV_EXCL}")
        set(ENV{MSYS2_ENV_CONV_EXCL}
            "$ENV{MSYS2_ENV_CONV_EXCL};INCLUDE;LIB;LIBPATH")
    else()
        set(_snow_x264_msys2_env_conv_excl_was_defined FALSE)
        set(ENV{MSYS2_ENV_CONV_EXCL} "INCLUDE;LIB;LIBPATH")
    endif()
endif()

vcpkg_make_configure(
    SOURCE_PATH "${SOURCE_PATH}"
    DISABLE_CPPFLAGS # Build is not using CPP/CPPFLAGS
    DISABLE_MSVC_WRAPPERS
    LANGUAGES ASM C CXX # Requires NASM to compile
    DISABLE_MSVC_TRANSFORMATIONS # disable warnings about unknown -Xcompiler/-Xlinker flags
    PRE_CONFIGURE_CMAKE_COMMANDS
        add_cross_prefix
    OPTIONS
        ${OPTIONS}
        --enable-pic
        --disable-lavf
        --disable-swscale
        --disable-avs
        --disable-ffms
        --disable-gpac
        --disable-lsmash
        --disable-bashcompletion
    OPTIONS_RELEASE
        ${OPTIONS_RELEASE}
        --enable-strip
        "--bindir=\\\${prefix}/bin"
    OPTIONS_DEBUG
        --enable-debug
        --disable-cli
        "--bindir=\\\${prefix}/bin"
)

vcpkg_make_install()

if(VCPKG_TARGET_IS_WINDOWS AND NOT VCPKG_TARGET_IS_MINGW)
    foreach(_snow_env_var IN ITEMS INCLUDE LIB LIBPATH)
        if(_snow_x264_${_snow_env_var}_was_defined)
            set(ENV{${_snow_env_var}} "${_snow_x264_${_snow_env_var}}")
        else()
            unset(ENV{${_snow_env_var}})
        endif()
    endforeach()
    if(_snow_x264_msys2_env_conv_excl_was_defined)
        set(ENV{MSYS2_ENV_CONV_EXCL} "${_snow_x264_msys2_env_conv_excl}")
    else()
        unset(ENV{MSYS2_ENV_CONV_EXCL})
    endif()
endif()

if("tool" IN_LIST FEATURES)
    vcpkg_copy_tools(TOOL_NAMES x264 AUTO_CLEAN)
endif()

file(REMOVE_RECURSE "${CURRENT_PACKAGES_DIR}/debug/include")

if(VCPKG_TARGET_IS_WINDOWS AND NOT VCPKG_TARGET_IS_MINGW)
    vcpkg_replace_string("${CURRENT_PACKAGES_DIR}/lib/pkgconfig/x264.pc" "-lx264" "-llibx264")
    if(NOT VCPKG_BUILD_TYPE)
        vcpkg_replace_string("${CURRENT_PACKAGES_DIR}/debug/lib/pkgconfig/x264.pc" "-lx264" "-llibx264")
    endif()
endif()

if(VCPKG_LIBRARY_LINKAGE STREQUAL "dynamic" AND VCPKG_TARGET_IS_WINDOWS AND NOT VCPKG_TARGET_IS_MINGW)
    file(RENAME "${CURRENT_PACKAGES_DIR}/lib/libx264.dll.lib" "${CURRENT_PACKAGES_DIR}/lib/libx264.lib")
    if (NOT VCPKG_BUILD_TYPE)
        file(RENAME "${CURRENT_PACKAGES_DIR}/debug/lib/libx264.dll.lib" "${CURRENT_PACKAGES_DIR}/debug/lib/libx264.lib")
    endif()
    vcpkg_replace_string("${CURRENT_PACKAGES_DIR}/include/x264.h" "#ifdef X264_API_IMPORTS" "#if 1")
elseif(VCPKG_LIBRARY_LINKAGE STREQUAL "static")
    vcpkg_replace_string("${CURRENT_PACKAGES_DIR}/include/x264.h" "defined(U_STATIC_IMPLEMENTATION)" "1" IGNORE_UNCHANGED)
    file(REMOVE_RECURSE
        "${CURRENT_PACKAGES_DIR}/bin"
        "${CURRENT_PACKAGES_DIR}/debug/bin"
    )
endif()

vcpkg_fixup_pkgconfig()

vcpkg_copy_pdbs()

vcpkg_install_copyright(FILE_LIST "${SOURCE_PATH}/COPYING")
